/// A type for which there is a [partial order](https://en.wikipedia.org/wiki/Partially_ordered_set).
/// The comparison must satisfy, for all `a`, `b` and `c`
/// * antisymmetry: if `a < b` then `not(a > b)``, as well as `a > b` implying `not(a < b)`
/// * transitivity: `a < b` and `b < c` implies `a < c`
/// The same must hold for both == and >

// see https://www.reddit.com/r/ProgrammingLanguages/comments/9u63pe/language_design_comparing_and_sorting/
public type PartiallyOrdered <: PartiallyEquatable
{
    public abstract fn compare(other: Self) -> Ordering?;

    public override operator == (other: Self) -> bool?
    {
        return self.compare(other) == .Equal;
    }

    public operator < (other: Self) -> bool
    {
        return self.compare(other) == .Less;
    }

    public operator <= (other: Self) -> bool
    {
        return match self.compare(other)
        {
            .Less | .Equal => true,
            _ => false
        };
    }

    public operator > (other: Self) -> bool
    {
        return self.compare(other) == .Greater;
    }

    public operator >= (other: Self) -> bool
    {
        return match self.compare(other)
        {
            .Greater | .Equal => true,
            _ => false
        };
    }
}
